apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'

group = 'net.samagames'
version = '5.0.0-SNAPSHOT'
archivesBaseName = 'SamaGamesCore'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'

configurations {
    provided
    compile.extendsFrom provided
}

configurations.all {
    // Check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url = "https://papermc.io/repo/repository/maven-public/"
    }
    maven {
        url = "https://repo.viaversion.com"
    }
    maven {
        url = "https://nexus.avadia.fr/repository/avadia-minecraft-release/"
        credentials {
            username avadiaUser
            password avadiaPassword
        }
    }
    maven {
        url = "https://nexus.avadia.fr/repository/avadia-minecraft-snapshot/"
        credentials {
            username avadiaUser
            password avadiaPassword
        }
    }
}

dependencies {
    compile(group: 'net.samagames', name: 'SamaGamesAPI', version: '5.0.0-SNAPSHOT', changing: true)
    compile(group: 'net.samagames', name: 'PersistanceAPI', version: '1.1.0-SNAPSHOT', changing: true)
    compile(group: 'redis.clients', name: 'jedis', version: '3.3.0')
    compile(group: 'in.ashwanthkumar', name: 'slack-java-webhook', version: '0.0.10')
    compileOnly(group: 'fr.farmvivi', name: 'api-common', version: '2.0.0.0-SNAPSHOT', changing: true)
    compileOnly(group: 'us.myles', name: 'viaversion', version: '3.1.1')

    compileOnly(group: 'org.spigotmc', name: 'spigot', version: '1.12.2-R0.1-SNAPSHOT', changing: true) {
        transitive = false
    }
    compileOnly(group: 'com.destroystokyo.paper', name: 'paper-api', version: '1.12.2-R0.1-SNAPSHOT', changing: true) {
        transitive = false
    }
}

sourceSets {
    main {
        java {
            srcDir 'PersistanceGeneratorCore/Generation'
        }
    }
}

jar {
    dependsOn configurations.runtime
    from {
        (configurations.runtime - configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

compileJava.dependsOn(':PersistanceGeneratorCore:runSimple')

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: 'https://nexus.avadia.fr/repository/avadia-minecraft-release/') {
                authentication(userName: avadiaUser, password: avadiaPassword)
            }
            snapshotRepository(url: 'https://nexus.avadia.fr/repository/avadia-minecraft-snapshot/') {
                authentication(userName: avadiaUser, password: avadiaPassword)
            }
        }
    }
}